using Compiler;
using System.Collections.Generic;

COMPILER Syntax

    public ProgramNode RootNode;



/*--------------------------------------------------------------------------------------*/

IGNORECASE

CHARACTERS
    whitespace      = " \t" .
    newline         = "\r\n" .
    notNewline      = ANY - newline .
    quote           = '"' .
    notQuote        = ANY - newline - '"' .
    digit           = "0123456789" .
    letter          = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_" .

TOKENS
    comment         = "//" {notNewline} .
    identifier      = letter {letter | digit} .
    func            = "func" .
    print           = "print" .
    return          = "return" .
    continue        = "continue" .
    break           = "break" .
    if              = "if" .
    else            = "else" .
    while           = "while" .
    var             = "var" .
    assignment      = "=" .
    equal           = "==" .
    notEqual        = "!=" .
    leq             = "<=" .
    geq             = ">=" .
    divide          = "/" .
    integer         = digit {digit} .
    string          = quote {notQuote} quote .
    
IGNORE
    "\t" + newline



/*--------------------------------------------------------------------------------------*/
 
PRODUCTIONS

Syntax =                        (. FunctionNode f; RootNode = new ProgramNode(); .)
(
    Function<out f>             (. RootNode.Functions.Add(f); .)
    {
        Function<out f>         (. RootNode.Functions.Add(f); .)
    }
).

Function<out FunctionNode f> =      (. IdentifierNode i; BlockStatementNode b; .)
(
    func 
    Identifier<out i>               (. f = new FunctionNode(i); .)
    '(' [
        Identifier<out i>           (. f.Parameters.Add(i); .)
        {
            ',' Identifier<out i>   (. f.Parameters.Add(i); .)
        }
    ] ')'
    Block<out b>                    (. f.Body = b; .)
).

Identifier<out IdentifierNode i> =
(
    identifier  (. i = new IdentifierNode(t.val); .)
).

Block<out BlockStatementNode b> =   (. DeclarationNode d; StatementNode s; b = new BlockStatementNode(); .)
(
    '{'
        {
            Declaration<out d>      (. b.Declarations.Add(d); .)
        }
        {
            Statement<out s>        (. b.Statements.Add(s); .)
        }
    '}'
).

Declaration<out DeclarationNode d> =    (. IdentifierNode i; d = new DeclarationNode(); .)
(
    var Identifier<out i>               (. d.Variables.Add(i); .)
    {
        "," Identifier<out i>           (. d.Variables.Add(i); .) 
    }
).

Statement<out StatementNode s> =    (. s = null; AssignmentStatementNode a; ReturnStatementNode r; PrintStatementNode p; NullStatementNode n; IfStatementNode i; WhileStatementNode w; BlockStatementNode b; .)
(
    AssignmentStatement<out a>      (. s = a; .)
    | ReturnStatement<out r>        (. s = r; .)
    | PrintStatement<out p>         (. s = p; .)
    | NullStatement<out n>          (. s = n; .)
    | IfStatement<out i>            (. s = i; .)
    | WhileStatement<out w>         (. s = w; .)
    | Block<out b>                  (. s = b; .)
).

AssignmentStatement<out AssignmentStatementNode a> =    (. IdentifierNode i; ExpressionNode e; .)
(
    Identifier<out i> assignment Expression<out e>      (. a = new AssignmentStatementNode(i, e); .)
).

ReturnStatement<out ReturnStatementNode r> =    (. ExpressionNode e; .)
(
    return Expression<out e>                    (. r = new ReturnStatementNode(e); .)
).

PrintStatement<out PrintStatementNode p> =      (. IPrintItemNode pi; p = new PrintStatementNode(); .)
(
    print PrintItem<out pi>                     (. p.Items.Add(pi); .)
    {
        "," PrintItem<out pi>                   (. p.Items.Add(pi); .)
    }
).

NullStatement<out NullStatementNode n> =    (. n = null; .)
(
    continue                                (. n = new NullStatementNode(NullStatementType.Continue); .)
    | break                                 (. n = new NullStatementNode(NullStatementType.Break); .)
).

PrintItem<out IPrintItemNode p> =   (. p = null; ExpressionNode e; StringNode s; .)
(
    Expression<out e>               (. p = e; .)
    | String<out s>                 (. p = s; .)
).

String<out StringNode s> =
(
    string                      (. s = new StringNode(t.val.Substring(1, t.val.Length - 2)); .)
).

Integer<out IntegerNode i> =
(
    integer                     (. i = new IntegerNode(t.val); .)
).

IfStatement<out IfStatementNode i> =    (. ExpressionNode condition; StatementNode thenBody, elseBody = null; .)
(
    if Expression<out condition> 
    Statement<out thenBody> 
    [ else Statement<out elseBody> ]    (. i = new IfStatementNode(condition, thenBody, elseBody); .)
).

WhileStatement<out WhileStatementNode w> =              (. ExpressionNode condition; StatementNode body; .)
(
    while Expression<out condition> Statement<out body> (. w = new WhileStatementNode(condition, body); .)
).

Expression<out ExpressionNode e> =      (. ExpressionNode left, right, current; .)
(
    Comparand<out left>                 (. e = current = left; .)
    {
          equal    Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.Equal, right); .)
        | notEqual Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.NotEqual, right); .)
        | '<'      Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.LessThan, right); .)
        | leq      Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.LessThanOrEqual, right); .)
        | '>'      Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.GreaterThan, right); .)
        | geq      Comparand<out right> (. e = current = new BinaryExpressionNode(current, Operator.GreaterThanOrEqual, right); .)
    }
).

Comparand<out ExpressionNode e> =   (. ExpressionNode left, right, current; .)
(
    Term<out left>                  (. e = current = left; .)
    {
          '+' Term<out right>       (. e = current = new BinaryExpressionNode(current, Operator.Add, right); .)
        | '-' Term<out right>       (. e = current = new BinaryExpressionNode(current, Operator.Subtract, right); .)
    }
).

Term<out ExpressionNode e> =            (. ExpressionNode left, right, current; .)
(
    Factor<out left>                    (. e = current = left; .)
    {
          '*'       Factor<out right>   (. e = current = new BinaryExpressionNode(current, Operator.Multiply, right); .)
        | divide    Factor<out right>   (. e = current = new BinaryExpressionNode(current, Operator.Divide, right); .)
    }
).

Factor<out ExpressionNode e> =          (. e = null; ExpressionNode child, arg; IntegerNode i; IdentifierNode name; FunctionCallNode f; .)
(
      '-' Expression<out child>         (. e = new UnaryExpressionNode(Operator.Subtract, child); .)
    | '(' Expression<out child> ')'     (. e = child; .)
    | Integer<out i>                    (. e = new ConstantExpressionNode(i); .)
    | Identifier<out name>              (. e = new VariableReferenceNode(name); .)
      [                                 (. e = f = new FunctionCallNode(name); .)
        '(' [
            Expression<out arg>         (. f.Arguments.Add(arg); .)
            {
                ',' Expression<out arg> (. f.Arguments.Add(arg); .)
            }
        ] ')'
      ]
).

END Syntax .
